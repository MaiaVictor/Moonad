Var : Type
  Pair(Char)(Nat)

Var.new : Char -> Nat -> Var
  Pair.new<Char><Nat>

Ctx : Type
  List(Var)

Ctx.nil : Ctx
  List.nil<Var>

Ctx.ext : Char -> Nat -> Ctx -> Ctx
  (nam) (x) List.cons<Var>(Var.new(nam)(x))

Ctx.get : Char -> Ctx -> Maybe(Nat)
  (chr) (ctx)
  ctx<() Maybe(Nat)>
  | Maybe.none<Nat>;
  | (h) (t)
    h<() Maybe(Nat)>
    | (nam) (var)
      U16.eql(nam)(chr)<() Maybe(Nat)>
      | Maybe.some<Nat>(var);
      | Ctx.get(chr)(t);;;

parse_fun.filt : String -> String
  String.filter((c) Bool.or
    | U16.eql(c)(Char.parse("位"));
    | U16.eql(c)(Char.parse("."));)

parse_fun.typ0 : String -> Ctx -> Type
  (str) (ctx)
  str<() Type>
  | Unit;
  | (nam) (nxt) parse_fun.typ1(nam)(nxt)(ctx);

parse_fun.typ1 : Char -> String -> Ctx -> Type
  (nam) (nxt) (ctx)
  nxt<() Type>
  | Nat;
  | (h) (t) (x: Nat) -> parse_fun.typ0(String.cons(h)(t))(Ctx.ext(nam)(x)(ctx));

parse_fun.term : (str:String) -> (ctx:Ctx) -> parse_fun.typ0(str)(ctx)
  (str) (ctx)
  str<(str) parse_fun.typ0(str)(ctx)>
  | Unit.new;
  | (nam) (nxt)
    nxt<(str) parse_fun.typ1(nam)(str)(ctx)>
    | Ctx.get(nam)(ctx)<() Nat>
      | Nat.zero;
      | (n) n;;
    | (h) (t) (x) parse_fun.term(String.cons(h)(t))(Ctx.ext(nam)(x)(ctx));;

fun : (str:String) -> parse_fun.typ0(parse_fun.filt(str))(Ctx.nil)
  (str) parse_fun.term(parse_fun.filt(str))(Ctx.nil)

crazy: Nat -> Nat -> Nat -> Nat
  fun("位x.位y.位z.y")

it_works: Equal(Nat)(Nat.1)(crazy(Nat.0)(Nat.1)(Nat.2))
  Equal.to<Nat><Nat.1>
  
  
